apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: "com.github.ben-manes.versions"


def bugsnagKey = project.hasProperty("britSummerBugsnagKey") ? project.property("britSummerBugsnagKey") : ""
def weatherApiKey = project.hasProperty("britSummerWeatherApiKey") ? project.property("britSummerWeatherApiKey") : ""

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.fractalwrench.britishsummer"
        minSdkVersion 28
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [
                bugsnagApiKey: bugsnagKey
        ]
    }
    buildTypes {
        debug {
            resValue "string", "weather_api_key", weatherApiKey
        }
        release {
            minifyEnabled true
            resValue "string", "weather_api_key", weatherApiKey
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }
    lintOptions {
        abortOnError true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply from: 'dependencies.gradle'
apply from: 'static-analysis.gradle'

// fail build on compiler warnings
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        allWarningsAsErrors = true
    }
}